<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:php="http://php.net/xsl" xmlns:lio="http://slothsoft.net"
    xmlns:func="http://exslt.org/functions" xmlns:date="http://exslt.org/dates-and-times" extension-element-prefixes="func date" xmlns:ssh="http://schema.slothsoft.net/schema/historical-games-night"
    xmlns:sfd="http://schema.slothsoft.net/farah/dictionary">

    <xsl:include href="farah://slothsoft@farah/xsl/dictionary" />

    <xsl:variable name="GFX_BASE" select="'/strayfarer@spieleabend.strayfarer.com/gfx/'" />

    <func:function name="lio:gfx-url">
        <xsl:param name="gfx" />
        <func:result select="concat($GFX_BASE, $gfx)" />
    </func:function>

    <xsl:variable name="MANUAL_BASE" select="'/strayfarer@spieleabend.strayfarer.com/manuals/'" />

    <func:function name="lio:manual-url">
        <xsl:param name="manual" />
        <func:result select="concat($MANUAL_BASE, $manual)" />
    </func:function>

    <xsl:variable name="LETTERS_LOWERCASE" select="'abcdefghijklmnopqrstuvwxyz'" />
    <xsl:variable name="LETTERS_UPPERCASE" select="'ABCDEFGHIJKLMNOPQRSTUVWXYZ'" />
    <xsl:variable name="LETTERS_REGION" select="'🇦🇧🇨🇩🇪🇫🇬🇭🇮🇯🇰🇱🇲🇳🇴🇵🇶🇷🇸🇹🇺🇻🇼🇽🇾🇿'" />

    <func:function name="lio:toRegionCode">
        <xsl:param name="region" />
        <func:result select="translate($region, $LETTERS_UPPERCASE, $LETTERS_REGION)" />
    </func:function>

    <func:function name="lio:toLowerCase">
        <xsl:param name="region" />
        <func:result select="translate($region, $LETTERS_UPPERCASE, $LETTERS_LOWERCASE)" />
    </func:function>

    <func:function name="lio:toUpperCase">
        <xsl:param name="region" />
        <func:result select="translate($region, $LETTERS_LOWERCASE, $LETTERS_UPPERCASE)" />
    </func:function>

    <func:function name="lio:protectSpace">
        <xsl:param name="string" />
        <func:result select="translate($string, ' -', '&#xA0;&#x2011;')" />
    </func:function>

    <xsl:variable name="ids" select="//ids/id" />

    <func:function name="lio:event-id">
        <xsl:param name="event" select="." />
        <func:result select="$ids[@name = $event/@theme]" />
    </func:function>

    <func:function name="lio:event-track">
        <xsl:param name="event" select="." />

        <xsl:variable name="id" select="$ids[@name = $event/@theme]" />
        <xsl:variable name="track" select="//ssh:track[@id = $id/@track]" />
        <xsl:variable name="subtrack" select="$track/ssh:subtrack[position() = $id/@subtrack-index]" />

        <func:result select="concat($track/@name, ' ', $id/@subtrack-index, format-number($id/@event-index, '00'), ' (', lio:protectSpace($subtrack/@name), ')')" />
    </func:function>

    <func:function name="lio:timestamp">
        <xsl:param name="date" />
        <xsl:param name="returnInfinite" select="false()" />
        <xsl:choose>
            <xsl:when test="contains($date, '-')">
                <xsl:variable name="year" select="number(substring($date,1,4))" />
                <xsl:variable name="month" select="number(substring($date,6,2))" />
                <xsl:variable name="day" select="number(substring($date,9,2))" />
                <func:result select="365 * $year + 31 * $month + $day" />
            </xsl:when>
            <xsl:when test="$returnInfinite">
                <func:result select="365 * 3000" />
            </xsl:when>
            <xsl:otherwise>
                <func:result select="0" />
            </xsl:otherwise>
        </xsl:choose>
    </func:function>

    <func:function name="lio:platform">
        <xsl:param name="id" select="." />
        <func:result select="//ssh:platform[@id = $id]" />
    </func:function>

    <func:function name="lio:subtrack">
        <xsl:param name="id" select="." />
        <func:result select="//ssh:subtrack[@id = $id]" />
    </func:function>

    <func:function name="lio:subtrack-name">
        <xsl:param name="id" select="." />
        <xsl:variable name="subtrack" select="//ssh:subtrack[@id = $id]" />
        <func:result select="concat($subtrack/../@name, ' - ', $subtrack/@name)" />
    </func:function>

    <func:function name="lio:event-datetime">
        <xsl:param name="event" select="." />

        <xsl:variable name="date" select="string($event/@date)" />

        <xsl:choose>
            <xsl:when test="contains($date, '-')">
                <xsl:variable name="year" select="substring($date,1,4)" />
                <xsl:variable name="month" select="substring($date,6,2)" />
                <xsl:variable name="day" select="substring($date,9,2)" />
                <func:result select="concat(sfd:lookup-text(date:day-name($date)), ', ', $day, '.', $month, '.', $year, ' ', $event/@time)" />
            </xsl:when>
            <xsl:otherwise>
                <func:result select="''" />
            </xsl:otherwise>
        </xsl:choose>
    </func:function>

    <func:function name="lio:event-datetime-discord">
        <xsl:param name="event" select="." />

        <xsl:variable name="datetime" select="concat($event/@date, ' ', $event/@time)" />

        <xsl:choose>
            <xsl:when test="contains($datetime, '-')">
                <xsl:variable name="timestamp" select="php:functionString('strtotime', $datetime)" />
                <func:result select="concat('&lt;t:', $timestamp, ':F&gt;')" />
            </xsl:when>
            <xsl:otherwise>
                <func:result select="''" />
            </xsl:otherwise>
        </xsl:choose>
    </func:function>

    <func:function name="lio:event-date">
        <xsl:param name="event" select="." />

        <xsl:variable name="date" select="string($event/@date)" />

        <xsl:choose>
            <xsl:when test="contains($date, '-')">
                <xsl:variable name="year" select="substring($date,1,4)" />
                <xsl:variable name="month" select="substring($date,6,2)" />
                <xsl:variable name="day" select="substring($date,9,2)" />
                <func:result select="concat($day, '.', $month, '.', $year)" />
            </xsl:when>
            <xsl:otherwise>
                <func:result select="''" />
            </xsl:otherwise>
        </xsl:choose>
    </func:function>

    <xsl:variable name="action" select="'#ffa793'" />
    <xsl:variable name="adventure" select="'#fff293'" />
    <xsl:variable name="strategy" select="'#a5d0d4'" />
    <xsl:variable name="transition" select="7" />

    <func:function name="lio:toBackgroundColor">
        <xsl:param name="red" />
        <xsl:param name="green" />
        <xsl:param name="blue" />
        <func:result select="concat(lio:toBackgorundColorCode($red), ', ', lio:toBackgorundColorCode($green), ', ', lio:toBackgorundColorCode($blue))" />
    </func:function>
    <func:function name="lio:toBackgorundColorCode">
        <xsl:param name="likert" />
        <func:result select="128 + $likert * 24" />
    </func:function>

    <func:function name="lio:toFontColor">
        <xsl:param name="red" />
        <xsl:param name="green" />
        <xsl:param name="blue" />
        <func:result select="'250, 250, 255'" />
    </func:function>
    <func:function name="lio:toFontColorCode">
        <xsl:param name="likert" />
        <func:result select="$likert * 48" />
    </func:function>

    <xsl:template match="ssh:event" mode="link" />

    <xsl:template match="ssh:event[@track]" mode="link">
        <xsl:variable name="ref" select="lio:event-id()" />
        <a href="#{$ref}" class="id">
            <xsl:value-of select="lio:event-track()" />
            <xsl:text>:</xsl:text>
        </a>
    </xsl:template>

    <xsl:template match="ssh:event" mode="global-link">
        <span class="id">
            <xsl:choose>
                <xsl:when test="parent::ssh:past | parent::ssh:present">
                    <xsl:attribute name="data-wanted"><xsl:value-of select="lio:event-date()" /></xsl:attribute>
                </xsl:when>
                <xsl:when test="parent::ssh:future">
                    <xsl:attribute name="data-wanted">READY</xsl:attribute>
                </xsl:when>
                <xsl:when test="parent::ssh:unfinished">
                    <xsl:attribute name="data-wanted"></xsl:attribute>
                </xsl:when>
            </xsl:choose>
            <xsl:value-of select="@theme" />
        </span>
    </xsl:template>

    <xsl:template match="ssh:event[@track-disabled]" mode="global-link">
        <xsl:variable name="ref" select="lio:event-id()" />
        <span class="id">
            <xsl:choose>
                <xsl:when test="parent::ssh:past | parent::ssh:present">
                    <xsl:attribute name="data-wanted"><xsl:value-of select="lio:event-date()" /></xsl:attribute>
                </xsl:when>
                <xsl:when test="parent::ssh:future">
                    <xsl:attribute name="data-wanted">READY</xsl:attribute>
                </xsl:when>
                <xsl:when test="parent::ssh:unfinished">
                    <xsl:attribute name="data-wanted"></xsl:attribute>
                </xsl:when>
            </xsl:choose>
            <xsl:value-of select="concat('[', $ref, '] ')" />
            <xsl:value-of select="@theme" />
        </span>
    </xsl:template>

    <xsl:template match="ssh:req" mode="link">
        <xsl:variable name="ref" select="string(@ref)" />
        <a href="#{$ref}" class="id" title="{lio:lookup-name($ref)}" onclick="document.querySelectorAll('h1 ~ details').forEach(d => d.open = true)">
            <xsl:attribute name="data-prereq">
                <xsl:if test="id($ref)/parent::ssh:future">missing</xsl:if>
            </xsl:attribute>
            <xsl:value-of select="lio:lookup-name($ref)" />
        </a>
    </xsl:template>

    <xsl:template match="ssh:read" mode="link">
        <a href="{@href}" target="_blank" rel="external">
            <xsl:value-of select="concat(@name, ' (', @by, ', ', @released, ')')" />
        </a>
    </xsl:template>

    <func:function name="lio:format-id">
        <xsl:param name="ref" />
        <xsl:variable name="id" select="substring($ref, 1, 3)" />
        <xsl:variable name="no" select="substring($ref, 4, 3)" />
        <xsl:variable name="name" select="//ssh:track[@xml:id=$id]/@name" />

        <func:result>
            <xsl:value-of select="$id" />
            <xsl:text>&#160;</xsl:text>
            <xsl:value-of select="$no" />
        </func:result>
    </func:function>

    <func:function name="lio:format-name">
        <xsl:param name="ref" />
        <xsl:variable name="id" select="substring($ref, 1, 3)" />
        <xsl:variable name="no" select="substring($ref, 4, 3)" />
        <xsl:variable name="track" select="//ssh:track[@xml:id=$id]" />
        <xsl:variable name="subtrack" select="$track/ssh:subtrack[position() = substring($no, 1, 1)]" />
        <func:result>
            <xsl:value-of select="$track/@name" />
            <xsl:text>&#160;</xsl:text>
            <xsl:value-of select="$no" />
            <xsl:if test="$subtrack">
                <xsl:text> (</xsl:text>
                <xsl:value-of select="$subtrack/@name" />
                <xsl:text>)</xsl:text>
            </xsl:if>
        </func:result>
    </func:function>

    <func:function name="lio:lookup-name">
        <xsl:param name="ref" />
        <xsl:variable name="id" select="substring($ref, 1, 3)" />
        <xsl:variable name="no" select="substring($ref, 4, 3)" />

        <func:result>
            <xsl:value-of select="lio:format-name($ref)" />
            <xsl:text>: </xsl:text>
            <xsl:value-of select="id($ref)/@theme" />
        </func:result>
    </func:function>

    <func:function name="lio:wiki">
        <xsl:param name="term" select="." />
        <func:result>
            <xsl:text>https://en.wikipedia.org/w/index.php?title=Special:Search&amp;search=</xsl:text>
            <xsl:value-of select="php:functionString('urlencode', normalize-space($term))" />
        </func:result>
    </func:function>

    <func:function name="lio:param">
        <xsl:param name="key" />
        <xsl:param name="val" />
        <func:result select="concat(php:functionString('urlencode', normalize-space($key)), '=', php:functionString('urlencode', normalize-space($val)))" />
    </func:function>

    <xsl:template name="wiki">
        <xsl:param name="term" select="." />
        <xsl:param name="wiki" select="''" />
        <xsl:choose>
            <xsl:when test="string($wiki) = ''">
                <a href="{lio:wiki($term)}" target="_blank" rel="external">
                    <xsl:value-of select="$term" />
                </a>
            </xsl:when>
            <xsl:otherwise>
                <a href="{$wiki}" target="_blank" rel="external">
                    <xsl:value-of select="$term" />
                </a>
            </xsl:otherwise>
        </xsl:choose>
    </xsl:template>
</xsl:stylesheet>